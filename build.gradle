plugins {
  id("java")
  id("idea")
  id("org.springframework.boot") version "3.3.2"
  id("io.spring.dependency-management") version "1.1.6"
  id("net.researchgate.release") version "3.0.2"
  id("com.google.protobuf") version "0.9.4"
}

group = 'se.vgregion.k8s'
sourceCompatibility = '21'

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  maven { url 'https://repo.spring.io/snapshot' }
	mavenCentral()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-webflux'
  implementation "com.github.javafaker:javafaker:1.0.2"
  implementation "io.dekorate:kubernetes-spring-starter:4.1.4"
  annotationProcessor "io.dekorate:kubernetes-annotations:4.1.4"

  compileOnly 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'

  //grpc stuff
  implementation 'net.devh:grpc-server-spring-boot-starter:3.1.0.RELEASE'
  implementation "com.google.protobuf:protobuf-java:4.27.2"
  implementation "io.grpc:grpc-stub:1.43.0"

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'io.projectreactor:reactor-test'
}

test {
  useJUnitPlatform()
}

release {
  git {
    requireBranch = 'main'
  }
}

protobuf {

  //generatedFilesBaseDir = "$buildDir/generated-main-proto-java"

  // Configure the protoc executable
  protoc {
    // Download from repositories
    artifact = 'com.google.protobuf:protoc:3.17.3'
  }
  plugins {
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:1.43.0'
    }
  }
  generateProtoTasks {
    all()*.plugins { grpc {} }
  }
}

sourceSets {
  main {
    java {
      srcDirs 'build/generated/source/proto/main/grpc'
      srcDirs 'build/generated/source/proto/main/java'
    }
  }
}

springBoot {
  buildInfo()
}